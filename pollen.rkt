#lang racket
(require pollen/decode txexpr)
(require pollen/setup)
(require pollen/core)
(require pollen/pagetree)

(provide (all-defined-out)) ;make everything global in this file

(define (practice-url name)
  (string-append "https://jestlearn.com/how_to_code/#" name))

; String -> List<(String, String)>
; read the contents of the filename from "starter" and "solution" directores and produce List<(FilePath, Contents)>
(define (starter&solution filename)
  ; Path -> String
  ; returns the file string with the first 3 lines if it contains racket teaching lang metadata
  (define (strip-meta path)
    (define contents (file->lines path #:line-mode 'any))
    (cond [(<= (length contents) 3) contents]
          [else
           (define third-line (third contents))
           (define has-metadata? (and (>= (string-length third-line) 8) (string=? (substring third-line 0 7) "#reader")))
           (string-join (if has-metadata?
                            (drop contents 3)
                            contents) "\n")]))

  (define starter-fname (string-append "starter/" filename "_starter.rkt"))
  ; (println (file->string starter-fname))
  ; (println (strip-meta starter-fname))
  ; (println (file->lines starter-fname #:line-mode 'return-linefeed))
  (define solution-fname (string-append "solutions/" filename "_solution.rkt"))
  (define file-contents (map strip-meta (list starter-fname solution-fname)))
  (list (list starter-fname (car file-contents))
        (list solution-fname (cadr file-contents))))


; String(s) -> txexpr
; creates codeblocks with optional data src for download
; (racket-code-block #:data-src "mypath.rkt" "foo" "baz" "bar")
(define (racket-code-block #:data-src [src ""] . contents)
  (txexpr 'pre
          (if (non-empty-string? src)
              `((class "line-numbers match-braces rainbow-braces") (data-src ,src) (data-download-link ""))
              `((class "line-numbers match-braces rainbow-braces")))
          (list (txexpr 'code '((class "language-racket")) contents))))

(define (c-code-block #:data-src [src ""] . contents)
  (txexpr 'pre
          (if (non-empty-string? src)
              `((class "line-numbers match-braces rainbow-braces") (data-src ,src) (data-download-link ""))
              `((class "line-numbers match-braces rainbow-braces")))
          (list (txexpr 'code '((class "language-C")) contents))))

; String -> Txexpr
; short for "racket-1-line" produces a racket 1 liner of code
(define (r1-liner . contents)
  (txexpr 'code '((class "language-racket")) contents))

; String -> txexpr
(define (code-problem filename)
  (define files (starter&solution filename))
  (define starter (car files))
  (define starter-path (car starter))
  (define starter-contents (cadr starter))
  (define solution (cadr files))
  (define solu-path (car solution))
  (define solu-contents (cadr solution))
  ;todo: add option to keep the Q block open?
  (q (string-append "Exercise " filename) (racket-code-block #:data-src starter-path starter-contents)
     (q "Answer" (racket-code-block #:data-src solu-path solu-contents)))
  )

; string -> txexpr
; returns a search link to the search of the racket docs
(define (rk op)
  (txexpr 'a (list (list 'href (string-append "https://docs.racket-lang.org/search/index.html?q=" op)))
          (list (txexpr 'code empty
                        (list op)))))

;todo: introduce (slide-only) content? that is invis in reading mode and only vis in slide_mode ?
; to active slidemode, hit f12 and run the global function slide_mode()
(define (slide . contents)
  (txexpr 'section '((class "slide")) contents))

;(define command-char #\◊)
(module setup racket/base
  (provide (all-defined-out))
  (require pollen/setup)
  (define project-server-port 1111)
  (define publish-directory (path->complete-path (build-path 'up "publish")))
  (define current-project-root (current-directory))
  (define block-tags (append (list 'summary) default-block-tags))) ;tags that aren't wrapped in p

(define (root . elements)
  (txexpr 'root empty (decode-elements elements
                                       #:exclude-tags (list 'code 'pre 'a 'table 'figure 'figcaption) ; do not do smartquotes or dashes in code blocks or tables
                                       #:txexpr-elements-proc (compose1 decode-paragraphs)
                                       #:string-proc (compose1 smart-quotes smart-dashes))))

;Number -> List<Number>
; returns a list of powers of two
(define (pot n)
  (cond [(= n 0) '()]
        [else (cons (expt 2 (- n 1)) (pot (- n 1)))]))

; String or Number -> Txexpr
;returns 2^n rendered correctly in html
(define (2^ n #:style [style '()])
  (define s
    (cond [(string? n) n]
          [(number? n) (number->string n)]))
  (txexpr 'span style (list "2" (txexpr 'sup empty (list s)))))

; Number -> List<txexpr>
; returns a list of powers of 2 html subscript
(define (two-subscripts n)
  (cond [(= n 0) '()]
        [else (cons (2^ (- n 1)) (two-subscripts (- n 1)))]))

;List<T> -> thead<tr<th>>
; takes a list of values and produces table headings
(define (table-headings l #:style [style empty])
  (txexpr 'thead style (list (txexpr 'tr empty (map (lambda (i)
                                                      (txexpr 'th empty (list (cond [(number? i) (number->string i)]
                                                                                    [(char? i) (string i)]
                                                                                    [else i])))) l)))))

;List<List<T>> or List<T>...N -> tbody<tr<td>>
; takes a list of values and produces table entries
(define (table-entries . l)
  (define c (first (first l))) ; Primitive or list
  (define convert-to-2d (cond
                          [(and (list? c) (symbol? (first c))) l] ;if already list and first el is a symbol
                          [(list? c) (car l)] ; if called like (list (list d1 d2) (list d3 d4)) ; already defined each row in a list
                          [else l])) ; if called (list 1 2 3) (list 1 2 3) in which case already a 2d list because of rest
  (define (process-items row) (map (lambda (i)
                                     (txexpr 'td empty (list (cond [(number? i) (number->string i)]
                                                                   [(char? i) (string i)]
                                                                   [else i])))) row))
  (txexpr 'tbody empty (map (lambda (r) (txexpr 'tr empty (process-items r))) convert-to-2d)))

;thead<tr<th>> -> xml table
; can take 1 heading, or list of them
(define (table headings #:caption [caption empty] #:style [style empty] . entries)
  (txexpr 'table style
          (append (match caption
                    [s #:when (string? caption) (list (txexpr 'caption empty (list s)))]
                    [s #:when (and (not (empty? caption)) (symbol? (car caption))) (list (txexpr 'caption empty (list s)))]
                    [_ (list (txexpr 'caption empty caption))])
                  (cond [(symbol? (car headings)) (list headings)]
                        [else headings]) entries)))

;String String -> txexpr
(define (q heading . content)
  (txexpr 'details empty (append (list (txexpr 'summary '((class "question")) (list heading))) content)))

;String String -> txexpr
(define (step heading . content)
  (txexpr 'details '((open "")) (append (list (txexpr 'summary '((class "step")) (list heading))) content)))

;accepts only the uid, not the full url
; https://youtu.be/fQnUTmOu3lc?t=1777
; String String Bool -> txexpr
; link to url, optional headline, optional bool
(define (yt link #:headline [headline "Youtube"] #:open [open #false])
  (define split (string-split link "?"))
  (define cleaned-link-for-thumbnail (if (cons? split) (car split) link))
  (define start-open (if open '((open "")) null))
  (define sdoc-string
    (string-append "<style>*{padding:0;margin:0;overflow:hidden}html,body{height:100%}img,span{position:absolute;width:100%;top:0;bottom:0;margin:auto}span{height:1.5em;text-align:center;font:48px/1.5 sans-serif;color:white;text-shadow:0 0 0.5em black}</style><a href=https://www.youtube.com/embed/" link "?autoplay=1><img src=https://img.youtube.com/vi/" cleaned-link-for-thumbnail "/hqdefault.jpg><span>▶</span></a>"))
  (txexpr
    'details
    start-open
    (list
      (txexpr
        'summary '((style "color:red;")) (list headline))
      (txexpr
        'iframe
        `((srcdoc ,sdoc-string)
          (allow "picture-in-picture")
          (allowfullscreen "true")
          (loading "lazy")
          (width "560")
          (height "315"))
        empty))))

; String -> String
; flips the bits
(define (bnot s)
  (define (flip i) (if (char=? i #\0) #\1 #\0))
  (list->string (map flip (string->list s))))

; String String -> String
; add two binary numbers and returns a binary sum
(define (binary-sum a b)
  (~r (+ (string->number (string-append "#b" a))
         (string->number (string-append "#b" b))) #:base 2))

;Number -> List<Char>
;use padded version for padding negative numbers
; NEGATIVE NUMBERS HAVE TO ACCOUNT FOR BIT WIDTH
(define (deci->binary n #:width [width 0])
  (define conv (~r #:base 2 (abs n)))
  ; Number -> Binary String
  (define (pad n)
    (define pad-zeros (make-list (abs (- (string-length conv) width)) #\0))
    (cond
      [(positive? n) (string-append (list->string pad-zeros) conv)]
      [(negative? n)
       (define flipped (bnot (list->string (append pad-zeros (string->list conv)))))
       (string-append "-" (binary-sum flipped "1"))]
      ))
  (define t (cond
              [(= n 0) (make-string width #\0)]
              [(positive? width) (pad n)]
              [(and (= width 0) (negative? n)) (raise-user-error "must provide #:width to convert negative")]
              [else
               conv]))
  (string->list t))

; number number -> txexpr
; 101011 -> 101011_2 where _2 will be printed in a subscript form
(define (base n n2)
  (txexpr 'span empty
          (list (if (number? n) (number->string n) n)
                (txexpr 'sub empty (list (cond [(number? n2) (number->string n2)]
                                               [else n2]))))))

;String -> txexpr
;returns hashlink id of an h2
(define (h2 s)
  (define slug (string-downcase (string-replace s " " "_")))
  (txexpr
    'h2
    `((id ,slug))
    (list
      (txexpr 'a
              `((class "anchor") (href ,(string-append "#" slug)))
              '("#"))
      s)))

(define (h3 s)
  (define slug (string-downcase (string-replace s " " "_")))
  (txexpr
    'h3
    `((id ,slug))
    (list
      (txexpr 'a
              `((class "anchor") (href ,(string-append "#" slug)))
              '("#"))
      s)))

(define (lead . contents)
  (txexpr 'div '((class "lead")) contents))

;List<T..N> -> List<T..N>
;removes special characters and converts racket data types to strings from list
(define (clean l)
  (define s1 (filter (lambda (x) (if (and (string? x) (string=? x "\n")) #f #t)) l))
  (map (lambda (x) (cond [(number? x) (number->string x)]
                         [else x])) s1))

;...T -> <li>i</li>
; takes a racket list and convers them to txexpr html list of items
(define (list->li . l)
  (define first (car l))
  (define (aux i) (map (lambda (x) (txexpr 'li empty (list x))) i))
  (cond [(list? first) (aux (clean first))]
        [else (aux (clean l))])
  )

(define (steps . l)
  (txexpr 'ol (list '(class "steps")) (list->li l)))

;Number Number -> Table
; converts a number in decimal to binary with a small bit table
; supports highlighting msb for negative numbers
(define (deci->bin-table num bits #:caption [caption empty])
  (define bin (deci->binary num #:width bits))
  (define headings (pot bits))
  (define discrim (cond [(positive? num) (cons headings bin)]
                        [(negative? num) (cons (append (list (highlight-first headings)) (rest headings))
                                               (append (list (highlight-first (rest bin))) (rest (rest bin))))]))
  (define h (car discrim))
  (define e (cdr discrim))
  (table #:caption caption (table-headings h) (table-entries e)))

; highlight first element
; List<T> -> txexpr<span T>
(define (highlight-first lst)
  (define t (car lst))
  (define discrim (cond [(number? t) (number->string t)]
                        [(char? t) (string t)]
                        [else t]))
  (txexpr 'span '((style "color:red;")) (list discrim)))

;highlight the first element red
; List<Char> -> Txexpr<Span "" <Span "">>
(define (highlight-msb lst)
  (txexpr 'span empty (list (txexpr 'span '((style "color:red;")) (list (list->string (list (car lst))))) (list->string (rest lst)))))

; Number -> List<List<String Number Number>>
; returns the binary division string method of converting decimal to binary
(define (deci->bin-div n)
  (define (aux n acc)
    (define quo (quotient n 2))
    (cond [(= n 0) acc]
          [else
           (aux quo (cons (list (string-append (number->string n) "/2") quo (remainder n 2)) acc))]))
  (reverse (aux n '())))

(define (layout-spread-row . items)
  (txexpr 'div '((style "display:flex; justify-content: space-evenly;")) items))

; string path relative to image folder, e.g "/images"
(define (img-row #:width [width "10vh"] . paths)
  (txexpr 'div '((style "display:flex; justify-content: space-evenly; flex-wrap:wrap;")) (map (lambda (i) (txexpr 'img (list (cons 'src (cons i empty)) '(style "width: 10vh")) empty)) paths)
          ))

(define (max-bit-table f caption)
  (table #:caption caption (table-headings (list "Decimal" "Binary")) (table-entries (f '()))))

(define (max-unsigned-limit-table bits)
  (define MAX (expt 2 bits))
  (define (aux acc)
    (define LEN (length acc))
    (cond [(= LEN MAX) (reverse acc)]
          [else
           (aux (cons (list LEN (list->string (deci->binary LEN #:width bits))) acc))]))
  (max-bit-table aux "unsigned"))

(define (max-signed-limit-table bits)
  (define MAX (expt 2 bits))
  (define MAX-NEG (/ MAX -2))
  (define (aux acc)
    (define LEN (length acc))
    (define count (+ MAX-NEG LEN))
    (cond [(= LEN MAX) (reverse acc)]
          [else
           (define r (deci->binary count #:width bits))
           (aux (cons (list count (cond [(negative? count) (highlight-msb (rest r))]
                                        [else (list->string r)])

                            ) acc))]))
  (max-bit-table aux "signed"))

;alias to html <time> because racket already reserves namespace
(define (ptime #:style [style empty] . c)
  (txexpr 'time style c))

; String -> String
; example.html -> example
(define (trim-ext s)
  (string-replace s (bytes->string/utf-8 (path-get-extension s)) "" #:all? #f))

(define ex '(pagetree-root (introduction.html installation.html) (misc/evolvedsimplicity.html misc/effective_education.html misc/howtowritecleancode.html misc/badui.html misc/learning_stages.html)))

; List<'page-root List<Symbol>>
; takes a pollen ptree pageroot
(define (generate-toc root)
  ; List<Symbols> -> List<(String List<txexpr> Symbol)
  ; returns: (list h1 (list h2s...) 'filename.html)
  (define (get-headings path)
    (define imp (dynamic-require (string-append (symbol->string path) ".pm") 'doc))
    (define hd (select 'h1 imp))
    (define subs (select* 'h2 imp))
    (cond [(string? hd) (list hd (if (list? subs) subs null) path)]
          [else (raise-user-error (string-append "there was no h1 on: " (symbol->string path))) ""]))

  (define (wrap j)
    (define heading (first j))
    (define filename (symbol->string (last j)))
    (define subs (second j))
    (define h2s (map (lambda (anc title)
                       (define attrs (get-attrs anc))
                       (define hashed-url (second (second attrs)))
                       (define updated-url (string-append filename hashed-url))
                       (define updated-attrs (list (cons 'href (list updated-url))))
                       ; (println updated-attrs)
                       ; (println anc)
                       (define updated-anc (txexpr (get-tag anc) updated-attrs (list title)))
                       ; (println updated-anc)
                       (txexpr 'li empty (list updated-anc)))
                     (filter-not string? subs) (filter string? subs)))
    (txexpr 'li empty
            (list (txexpr 'a (list (cons 'href (list filename)))
                          (list heading
                                (txexpr 'ul empty h2s))))))

  ; List<Symbol> -> txexpr
  ; '(intro.html)
  (define (section items)
    (define headings (map get-headings items))
    (define section-heading (first (first headings)))
    (txexpr 'li empty (list (h2 section-heading) (txexpr 'ol '((start "0")) (map wrap headings)))))
  (txexpr 'ol '((start "0")) (map section (cdr root))))

(define ptree-example '((introduction.html learning_stages.html installation.html) (expressions.html exercise_pythag.html strings.html images.html rtfm.html variables.html functions.html parens_error.html stepper.html)))
;(generate-toc (list (list 'misc/EvolvedSimplicity.html)))
;(generate-toc (list (list (build-path (current-directory) "misc" "EvolvedSimplicity.html"))))

(define (stack-step title . content)
  `(div ((class "stack-tep-container"))
        (h3 ,title)
        (pre ((class "")) ,@content)))

(define (red x) (txexpr 'span '((style "color:red;")) (list x)))
(define (blue x) (txexpr 'span '((style "color:blue;")) (list x)))
(define (green x) (txexpr 'span '((style "color:green;")) (list x)))